include 'config.ttl'

PYTHON_EXECUTABLE = PROJECT_PATH
strconcat PYTHON_EXECUTABLE '\'
strconcat PYTHON_EXECUTABLE PYTHON_VENV
strconcat PYTHON_EXECUTABLE '\Scripts\python.exe'

SCRIPT_PATH = PROJECT_PATH
strconcat SCRIPT_PATH '\generate_otp.py'

OUTPUT_FILE = PROJECT_PATH
strconcat OUTPUT_FILE '\otp_output'

; 二段階認証を取得するpythonスクリプトを実行するcommandを組み立て
COMMAND_EXEC = '"'
strconcat COMMAND_EXEC PYTHON_EXECUTABLE
strconcat COMMAND_EXEC '" "'
strconcat COMMAND_EXEC SCRIPT_PATH
strconcat COMMAND_EXEC '" "'
strconcat COMMAND_EXEC OUTPUT_FILE
strconcat COMMAND_EXEC '" "'
strconcat COMMAND_EXEC AUTH_SECRET
strconcat COMMAND_EXEC '"'

; OUTPUT_FILEが存在する場合は削除
filesearch OUTPUT_FILE
if result=1 filedelete OUTPUT_FILE

; 接続コマンドオプション組立て
COMMAND = HOSTADDR
strconcat COMMAND ':22 /ssh /2 /auth=password /user='
strconcat COMMAND USERNAME
strconcat COMMAND ' /passwd='
strconcat COMMAND PASSWORD
strlen PROXY
if result>0 then
  strconcat COMMAND ' /proxy='
  strconcat COMMAND PROXY
endif

; 接続
connect COMMAND
pause 2

; 二段階認証コードを送信
wait 'Verification code:'

; Pythonスクリプトを実行して出力をファイルに出力
exec COMMAND_EXEC
pause 1
; ファイルオープン
fileopen fhandle OUTPUT_FILE 0
; 一行読み込み
filereadln fhandle TWOFA_CODE
; ファイルクローズ
fileclose fhandle
;pause 1

sendln TWOFA_CODE
filedelete OUTPUT_FILE

end